#|
 This file is a part of 3d-vectors
 (c) 2015 Shirakumo http://tymoon.eu (shinmera@tymoon.eu)
 Author: Nicolas Hafner <shinmera@tymoon.eu>
|#

(in-package #:3d-vectors.f)

(eval-when (:compile-toplevel :load-toplevel)
  (defvar *float-type* 'single-float))

(deftype float-type ()
  '#.*float-type*)

(declaim (inline ensure-float))
(declaim (ftype (function (real) #.*float-type*)))
(defun ensure-float (thing)
  (declare (optimize (speed 1)))
  (coerce thing '#.*float-type*))

(defun ensure-float-param (val env)
  (if (constantp val env)
      (typecase val
        (real (ensure-float val))
        (T `(load-time-value (ensure-float ,val))))
      `(locally (declare (optimize (speed 1))) (ensure-float ,val))))

(defmacro define-ofun (name args &body body)
  `(progn
     #+sbcl (declaim (sb-ext:maybe-inline ,name))
     (defun ,name ,args
       (declare (optimize (compilation-speed 0) (debug 1) (safety 1) speed))
       ,@body)))

(defmacro defsetf* (name args values &body body)
  #-(or ccl abcl ecl)
  `(defsetf ,name ,args ,values ,@body)
  #+(or ccl abcl ecl) ;; Compiler bug workarounds, hooray.
  (let ((args (loop for arg in args
                    until (eql arg '&environment)
                    collect arg))
        (env (loop for arg = (pop args)
                   while arg
                   do (when (eql arg '&environment)
                        (return (pop args))))))
    `(defsetf ,name ,args ,values
       ,@(if env
             `((let (,env)
                 ,@body))
             body))))

(defun intern* (&rest parts)
  (let ((*print-case* (readtable-case *readtable*))
        (*package* #.*package*))
    (intern (format NIL "狺疳螋螬．疳汶徵濯┅ㄤ彐磲泸溴骈铄痿蜴躅钺礤豉疱骈蝮舡箪雉＋筲沆戾è钺礤徜潋ㄩ铘弪瞠钺礤п滗颟┅啜痱镧筲愫轰彐轭瀛篝蝓泗躜瀛箪雉徜潋弩箫钺礤徜潋后趄蹉趱蝈豉疱后祜骈蝮舡箪雉ㄤ邈灬轫ㄩ铎轭钺礤┅ㄤ邈灬轫ㄦ豉疱ㄦ躅泗轱ì豉疱筲簌蠛簌篝屙狎遽痫轭翦颟钺礤┅ㄤ彐躅钺礤鲥悌筲簌蠛轭舡筢ì钺礤徜潋鲥悌┅┅ㄩ瞽疳汶徵：充鲥泗矧螽洎ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐ㄤ彐鲠骒镝舡豉疱т秕忪瀛骒镝舂ㄤ彐豉疱骒镝舡豉疱īВ骒镝舡豉疱ㄤ邈灬轫ㄩ铎轭孱篚蝈骒镝舂ㄤ邈灬轫ㄦ豉疱ㄦ躅泗轱蝈犰．骒镝舡豉疱┅ㄤ彐躅孱篚蝈骒镝翳轭绌ㄤ邈灬蝈镳糸黹箴邋暴┅ㄣ镥蜚翳轭В骒镝舡豉疱┅ㄤ彐躅孱篚蝈骒镝舡疳蜥鲠孱雯ㄩㄣ镱篝犷麴鲠孱雯豉疱汜箦鲠蝈犰ㄥ铙躜瀛骒镝鲠飑ㄔ啜祜徜糸礤鲠祯ㄥ铙躜瀛骒镝鲠飑┅啜祜汜祆ㄤ邈灬蝈镳糸黹箴邋暴┅ㄥ铙躜瀛骒镝鲠飑┅ㄤ彐磲泸溴骈铄镦躅钺礤狎珞怙澌怙澌啜痱镧＋筲沆ㄤ邈灬轫筲屮艉磲忮轭扉铄钺礤┅ㄤ彐躅钺礤狎珞ㄤ邈灬蝈镳糸黹ㄣ镯痖灬糸镱箴邋癌ㄤ邂蹒暴筢驽豉暴箴邋洎棱镤┅ㄤ彐磲泸溴骟弭妾钺礤狎珞鲠祯弩怙澌怙澌－矧沣徕沆邈飑啜溴骟弭钺礤狎珞鲠祯弩棱镤＋矧沣徕沆邈飑换蔑眇殪弪怩黠螂狎秕钿蟋栾矧狴戾è狎珞祜镳骘狎轭狎珞躅糸ㄥ耢狎Е孱鲩蝻铐孱舂泔祆邈狎绌ㄥ铞祜镳骘狎痫狎珞麒殪狎滹麒孱ㄥ耢狎Е孱鲩蝻铐孱舂蝈趱蝾痫狎珞┅┅┅啜溴骟弭钺礤狎珞鲠祯弩括殒孱啜戾ì孱雯棱镤┅怙澌┅┅ㄤ彐躅轭翦蝾é蝈篝疳螋螬戾è痱轭舡汜箦蝈徜翎忪瀛汜箦蝈徜翎忪濯┅í疳汶徵濯．疳汶徵濯┅ㄩ铘弪ㄦ矧磲紊狺疳螋螬．疳汶徵濯┅ㄤ彐磲泸溴骈铄痿蜴躅钺礤豉疱骈蝮舡箪雉＋筲沆戾è钺礤徜潋ㄩ铘弪瞠钺礤п滗颟┅啜痱镧筲愫轰彐轭瀛篝蝓泗躜瀛箪雉徜潋弩箫钺礤徜潋后趄蹉趱蝈豉疱后祜骈蝮舡箪雉ㄤ邈灬轫ㄩ铎轭钺礤┅ㄤ邈灬轫ㄦ豉疱ㄦ躅泗轱ì豉疱筲簌蠛簌篝屙狎遽痫轭翦颟钺礤┅ㄤ彐躅钺礤鲥悌筲簌蠛轭舡筢ì钺礤徜潋鲥悌┅┅